import "lib/errors";

|| @private
extern fun FileOpen(str filename, num mode) > num !> FileNotFoundError, FileSystemError, UnexpectedError;
|| @private
extern fun FileClose(num fd) > void;
|| @private
extern fun FileReadAll(num fd) > str !> ReadWriteError, FileSystemError, UnexpectedError;
|| @private
extern fun FileReadLine(num fd) > str? !> ReadWriteError, FileSystemError, UnexpectedError;
|| @private
extern fun FileRead(num fd, num n) > str? !> ReadWriteError, FileSystemError, InvalidArgumentError, UnexpectedError;
|| @private
extern fun FileWrite(num fd, str bytes) > void !> FileSystemError, ReadWriteError, UnexpectedError;
|| @private
extern fun getStdIn() > num;
|| @private
extern fun getStdOut() > num;
|| @private
extern fun getStdErr() > num;

|| File mode with which you can open a file
enum FileMode {
    read,
    write,
    update,
}

|| Object to manipulate an opened file
object File {
    || File descriptor
    num fd,

    || Open file
    || @param filename Path of file to open
    || @param mode Mode with which to open it
    || @return opened file
    static fun open(str filename, FileMode mode) > File !> FileNotFoundError, FileSystemError, UnexpectedError {
        return File {
            fd = FileOpen(filename, mode: mode.value),
        };
    }

    || Close file
    fun close() > void {
        FileClose(this.fd);
    }

    || Reads file until `EOF`
    fun readAll() > str !> ReadWriteError, FileSystemError, UnexpectedError {
        return FileReadAll(this.fd);
    }

    || Reads next line, returns null if nothing to read
    fun readLine() > str? !> ReadWriteError, FileSystemError, UnexpectedError {
        return FileReadLine(this.fd);
    }

    || Reads `n` bytes, returns null if nothing to read
    || @param n how many bytes to read
    fun read(num n) > str? !> ReadWriteError, FileSystemError, InvalidArgumentError, UnexpectedError {
        return FileRead(this.fd, n: n);
    }

    || Write bytes
    || @param bytes string to write
    fun write(str bytes) > void !> FileSystemError, ReadWriteError, UnexpectedError {
        FileWrite(this.fd, bytes: bytes);
    }
}

|| stdin
const File stdin = File { fd = getStdIn() };
|| stdout
const File stdout = File { fd = getStdOut() };
|| stderr
const File stderr = File { fd = getStdErr() };

|| Run a buzz file
|| @param filename path to buzz file
extern fun runFile(str filename) > void !> CompileError, InterpretError, FileSystemError, ReadWriteError;

export FileMode;
export File;
export stdin;
export stdout;
export stderr;
export runFile;