import "lib/std";

object JsonParser {
    str source,
    num offset = 0,

    fun advance() > str? {
        if (this.offset >= this.source.len()) {
            return null;
        }

        str char = this.source[this.offset];

        this.offset = this.offset + 1;

        return char;
    }

    fun peek() > str? {
        if (this.offset >= this.source.len()) {
            return null;
        }

        return this.source[this.offset];
    }

    fun match(str expected) > bool {
        if (this.offset > this.source.len()) {
            return false;
        }

        if (this.source[this.offset] != expected) {
            return false;
        }

        this.offset = this.offset + 1;

        return true;
    }

    fun consume(str expected) > void {
        if (!this.match(expected)) {
            throw "Could not parse JSON: expected `{expected}` got `{this.peek()}` at offset {this.offset}";
        }
    }

    fun skipWhitespaces() > void {
        while (true) {
            const str? char = this.peek();

            if (char == " " or char == "\r" or char == "\t") {
                this.advance();
            } else {
                return;
            }
        }
    }

    fun next() > Json {
        this.skipWhitespaces();

        if (this.offset >= this.source.len()) {
            throw "Could not parse JSON: end of string";
        }

        const str? char = this.advance();
        if (char == "[") {
            return Json{ list = this.array() };
        } else if (char == "\{") {
            return Json{ map = this.map() };
        } else if (char.byte(0) >= "0".byte(0) and char.byte(0) <= "9".byte(0)) {
            return Json{ number = this.number(char!) };
        } else if (char == "\"") {
            return Json{ string = this.string() };
        } else if (this.source.sub(this.offset - 1, len: 4) == "true") {
            this.offset = this.offset + 3;
            return Json{ boolean = true };
        } else if (this.source.sub(this.offset - 1, len: 5) == "false") {
            this.offset = this.offset + 4;
            return Json{ boolean = false };
        } else if (this.source.sub(this.offset - 1, len: 4) == "null") {
            this.offset = this.offset + 3;
            return Json{};
        }

        throw "Could not parse JSON: unexpected character `{char}` at offset {this.offset}";
    }

    fun array() > [Json] {
        [Json] array = <Json>[];

        while (true) {
            this.skipWhitespaces();

            if (this.match("]")) {
                break;
            }
                
            array.append(this.next());

            this.skipWhitespaces();

            if (this.match("]")) {
                break;
            } 

            this.consume(",");
        }

        return array;
    }

    fun map() > {str, Json} {
        {str, Json} map = <str, Json>{};

        while (true) {
            this.skipWhitespaces();

            if (this.match("}")) {
                break;
            }

            this.consume("\"");
            const str key = this.string();

            this.skipWhitespaces();

            this.consume(":");

            this.skipWhitespaces();

            map[key] = this.next();

            if (this.match("}")) {
                break;
            }
            this.consume(",");
        }

        return map;
    }

    fun number(str parsed) > num {
        str number = parsed;

        while (parseNumber(this.peek() ?? "NaN") != null) {
            str? char = this.advance();

            if (char == null) {
                break;
            }

            number = number + char!;
        }

        num? parsed = parseNumber(number);
        
        if (parsed == null) {
            throw "Could not parse JSON: `{number}` is not a number";
        }

        return parsed!;
    }

    fun string() > str {
        str? char = this.advance();
        str string = "";
        
        while (char != null and char != "\"") {
            if (char == "\\" and this.match("\"")) {
                string = string + "\"";
            } else {
                string = string + char!;
            }

            char = this.advance();
        }

        return string;
    }
}

|| Utility object to manage data from a JSON string
object Json {
    str? string = null,
    bool? boolean = null,
    num? number = null,
    {str, Json}? map = null,
    [Json]? list = null,

    fun stringValue() > str -> this.string ?? ""
    fun booleanValue() > bool -> this.boolean ?? false
    fun numberValue() > num -> this.number ?? 0
    fun mapValue() > {str, Json} -> this.map ?? <str, Json>{}
    fun listValue() > [Json] -> this.list ?? <Json>[]

    || Encode to a JSON string
    || @return str the JSON string
    fun encode() > str {
        if (this.string != null) {
            return "\"{this.string!}\"";
        } else if (this.boolean != null) {
            return "{this.boolean!}";
        } else if (this.number != null) {
            return "{this.number!}";
        } else if (this.map != null) {
            str json = "\{";
            const num size = this.map!.size();
            num count = 0;
            foreach (str key, Json value in this.map!) {
                json = json + "\"{key}\":{value.encode()}";

                if (count < size - 1) {
                    json = json + ",";
                }

                count = count + 1;
            }
            return json + "}";
        } else if (this.list != null) {
            str json = "[";

            const num len = this.list!.len();
            foreach (num i, Json value in this.list!) {
                json = json + value.encode();

                if (i < len - 1) {
                    json = json + ",";
                }
            }

            return json + "]";
        } else {
            return "null";
        }
    }

    || Decode string to a Json instance
    || @param str json The JSON string
    || @return Json
    static fun decode(str json) > Json {
        return JsonParser{ source = json }.next();
    }
}

export Json;